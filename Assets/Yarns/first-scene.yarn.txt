title: start
tags: 
colorID: 0
position: -1463,-480
---
<<if $CoachTalked == 1>>
    Coach: Please. The tournament is starting soon.
<<else>>
    <<set $CoachTalked to 1>>
    Coach: Wake up, Mr. Neddy. 
    <<protag_passed_out_eyes_open>>
    Coach: You have a rough night? Life's gonna be hard on you if this is what you look like on a Wednesday.
    <<hideui>>
    <<wait 1>>
    <<protag_still>>
    <<wait 1>>
    <<protag_vom>>
    Player: I feel a million times better. Thanks Coach P. I'll keep that in mind.
    Coach: Please get out of here. We are about to play a tournament.
    Player: No problem. Now which way is the exit again...? I never liked tennis courts bcause there's only one or two ways out. And if someone chains the gate... then what?
    Coach: Same way as you took in, son.
    Player: If only I could remember...
<<endif>>
===
title: tennis-players
tags: 
colorID: 0
position: -1146,-445
---
<<if $TennisTalked == 1>>
    Tennis1: We really need to warm up.
    Tennis2: We'll catch you later.
<<else>>
    <<set $TennisTalked to 1>>
    Tennis1: Waking up on the courts again? Classic.
    Tennis2: Hit rock bottom yet?
    Player: Not yet. Not even close... Listen, what period is it?
    Tennis2: You can probably still catch third period if you run.
    Tennis1: I have Advil... and oxycotton...
    -> Advil
        Player: I'll take some Advil.
    -> Oxycotton
        Player: I'll take an oxy.
<<endif>>
===
title: logan
tags: 
colorID: 0
position: -330,-474
---
Logan: Hey man. You're a little later than usual.
Player: Yeah, just a little. I love the smell of American Spirits in the morning.
Logan: Want one?
-> Sure
    Player: Sure. I never know how you can afford the expensive kind.
    <<protag_smoke>>
    Logan: Hard work. Bribery. Thievery. Pick one.
    -> Thievery
    -> Bribery
    -> Hard work
    Logan: Don't actually pick-- it's rhetorical dude.
    Player: right lol
-> Nah
    Player: I'm good.
    Logan: Oh. That's... weird.
    Player: Huh?
    Logan: It's nothing. You just... usually take it.
    Player: I guess?
Logan: You look like you slept outside. Where did you end up last night?
Player: Outside.
Logan: Nice.
<<hideui>>
<<wait 1>>
Logan: You missed Mr. Ham-and-Cheese lecture us on some dead guy named John Cheetos?
Player: John Cheetos?
Logan: Yup. Some writer or something
Player: Cheever.
Logan: God bless you.
Player: No. You mean John Cheever.
<<if $DiedOnce == 1>>
    [[CanWin]]
<<else>>
    <<set $DiedOnce to 1>>
    [[FirstTime]]
<<endif>>




===
title: CanWin
tags: 
colorID: 6
position: -561,-201
---
Logan: Sure, man. You're the genius. Still on for the party tonight? You should probably go home and change first. Or you know, go to class smelling like Lou Reed...
-> Go home
    Player: Yeah, I'm gonna go home. I'd be useless in class anyway.    
    <<if $PickedFirstTime == "home">>
        Logan: Wait, really?
        Player: Yeah?
        Logan: That's... it's just that you usually go to class...
        [[Die]]
    <<endif>>
-> Go to class
    Player: I might as well go to class.    
    <<if $PickedFirstTime == "class">>
        Logan: Wait, really?
        Player: Yeah?
        Logan: That's... it's just that you usually go to home...
        [[Die]]
    <<endif>>
Logan: Hm...
Logan: Okay.
Player: Why are you being weird?
<<hideui>>
<<logan_put_out_cig>>
Logan: I just have a lot on my mind.
Logan: Hey, one sec before you go. Have you ever heard of Simulation Theory?
-> Yes
    Player: Yeah, it's like we're all in the Matrix.
    Logan: Yeah, something like that. You ever think about it?
    Player: I mean... why does it matter? Even if it is, we'd never know, right?
    Logan: Yeah. Yeah, that's right. Sure.
-> No
    Player: What Theory?
    Logan: It's like... what if everything, the entire universe, is really just a computer simulation.
    Player: Like the Matrix?  
    Logan: Yeah, basically.
    Player: Who's running the simulation? Other people?
    Logan: Or aliens. Or I don't know... superdimensional beings that we can't comprehend.
    Player: Super what?
    Logan: Like...
    Logan: Nevermind.
Logan: It's just something I've been thinking about. Sorry about being weird. I gotta go back to class, I'll see you tonight, man.
<<win>>
===
title: Die
tags: 
colorID: 0
position: -306,148
---
Player: What do you mean "usually"? I don't usually wake up on the tennis courts.
<<hideui>>
<<logan_put_out_cig>>
Logan: This isn't how it's supposed to happen.
Player: What are you talking about, man.
Logan: Something's not right.
Player: Logan. What are you talking about?
<<hideui>>
<<logan_pull_out_gun>>
Logan: I guess we can try turning it off and on again.
<<protag_still>>
Player: WOAH. What the fuck dude! Where did you get a gun?? What are you doing??
Logan: Let's try this again.
<<loop>>
===
title: FirstTime
tags: 
colorID: 0
position: -79,-201
---
Logan: Sure, man. You're the genius. Still on for the party tonight? You should probably go home and change first. Or you know, go to class smelling like Lou Reed...
-> Go home
    <<set $PickedFirstTime to "home">>
    Player: Yeah, I'm gonna go home. I'd be useless in class anyway.
    Logan: Wait, really?
    Player: Yeah?
    Logan: That's... it's just that you usually go to class...
-> Go to class
    <<set $PickedFirstTime to "class">>
    Player: I might as well go to class.
    Logan: Wait, really?
    Player: Yeah?
    Logan: That's... it's just that you usually go home...
[[Die]]
===
